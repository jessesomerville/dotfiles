#!/usr/bin/env zsh

alias aliases="nvim $0"
alias cat="bat"
alias ccat="command cat"
alias dotf="git dotfiles"
alias hist="history -fD 0"  # Show history with time, date, and command runtime
alias la="ls -lAh --color=auto"
alias ls="ls --color=auto"
alias rg="rg --hidden"
alias rgi="rg -i"
alias top="btm -b"
alias vim="nvim"
alias lines="print -- ${(l:80::â”€:)}"
alias envs="env | rg -v '^LS_COLORS' | sort"
alias tv="tidy-viewer"
alias weather="curl -sSL 'http://wttr.in/New%20York'"
alias goinstall="curl -LO https://get.golang.org/$(uname)/go_installer && chmod +x go_installer && ./go_installer && rm go_installer"

randstr() { print -r -- $(tr -dc A-Za-z0-9 </dev/urandom | head -c ${1:-64}); }
dec2hex() { python3 -c "print(hex($1))"; }

echoinfo() { print -Pu2 "%B%F{12}[+]%f%b" $@; }
echoerr()  { print -Pu2 "%B%F{ 9}[!]%f%b" $@; }

cdgo() {
  local subdir="${1:-}"
  cd "$(go env GOPATH)/src/github.com/jessesomerville/${subdir}"
}

mkcd() {
  mkdir -p "${1:-}" || return 1
  cd "${1:-}"       || return 1
}

dhtml() {
  [[ -f "${1:-}" ]] || return 1
  lynx -dump "${1:-}" -nolist --width $(( $(tput cols) - 5 ))
}

hexcolor() {
  setopt localoptions extendedglob
  local hexval="#${(S)1#\#}"
  print -Pu2 "%B%F{$hexval} $hexval %f%K{$hexval} $hexval %k%b"
}

colors16() {
  local fgc bgc
  for i in {0..15}; do
    fgc="${fgc} %F{$i}${(l:2:: ::0:)i}%f "
    bgc="${bgc}%K{$i}${(l:4:)}%k"
    if (( (i + 1) % 16 == 0 )); then
      print -lP $fgc $bgc
      fgc='' bgc=''
    fi
  done
  print -lP $fgc $bgc
}

define() {
  local dictfile="$XDG_DATA_HOME/websters_dict_en.json"
  if [[ ! -f $dictfile ]]; then
    echoinfo "Downloading dictionary file"
    wget -O $dictfile 'https://github.com/matthewreagan/WebstersEnglishDictionary/raw/master/dictionary.json'
  fi
  local query_word="${1:-}"
  if [[ -z $query_word ]]; then
    echoerr "Missing required term argument"
    return 1
  fi

  local definition=$(jq -r '.'$query_word < $dictfile)
  if [[ -z $definition ]]; then
    echoerr "No definition found for $query_word"
    return 1
  fi
  print -P "%F{7}$(print -r -- $definition | sd -p '(\d+\.) ' '\n$1 ' | sd -p '^(\d+\. )(.*)$' '%F{cyan}$1%F{7}$2' | sd -p '\n\n' '\n')%f"
}
