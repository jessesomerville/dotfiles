#!/usr/bin/env zsh

alias aliases="nvim $0"
alias adb='HOME="$XDG_DATA_HOME/android" adb'
alias cat="bat"
alias ccat="command cat"
alias duckdb="duckdb -init $XDG_CONFIG_HOME/duckdb/duckdbrc"
alias wh='{ which "$(cat -)" | bat -l zsh --style=numbers; }<<<'
alias dotfiles="git --git-dir=$HOME/.dotfiles --work-tree=$HOME"
alias gmailctl="gmailctl --config=$XDG_CONFIG_HOME/gmailctl"
alias hist="history -fD 0"  # Show history with time, date, and command runtime
alias ls="ls --color=auto"
alias la="ls -lAh"
alias rg="rg --hidden"
alias rgi="rg -i"
alias top="btm -b"
alias vim="nvim"
alias lines="print -- ${(l:80::â”€:)}"
alias tv="tidy-viewer"
alias weather="curl -sSL http://wttr.in"
alias urlencode="python3 -c 'import urllib.parse, sys; print(urllib.parse.quote(sys.argv[1]))'"
alias wget="wget --hsts-file=$XDG_CACHE_HOME/wget-hsts"

alias -g -- --helpbat='--help |& bat -l help -p -P'

aliases[=]='noglob arith-eval'

randstr() { print -r -- $(tr -dc A-Za-z0-9 </dev/urandom | head -c ${1:-64}); }
dec2hex() { python3 -c "print(hex($1))"; }

cdgo() {
  local p=$(go env GOPATH)/src/github.com/jessesomerville
  [[ -d $p ]] || mkdir -p $p
  cd $p/${1:-}
}
compdef '_files -W $(go env GOPATH)/src/github.com/jessesomerville -/' cdgo

mkcd() {
  mkdir -p "${1:-}" || return 1
  cd "${1:-}"       || return 1
}

envs() {
  local width=$(tput cols)
  env | sort | awk '
    length($0) <= '$width' { print $0 }
    length($0) > '$width' { print substr($0, 1, '$(( width - 3 ))') "..." }' \
    | bat --plain --language sh --pager=never
}

dhtml() {
  [[ -f "${1:-}" ]] || return 1
  lynx -dump "${1:-}" -nolist --width $(( $(tput cols) - 5 ))
}

hexcolor() {
  local x="#${1#\#}"
  local fg="%F{$x} $x %f"
  local bg="%K{$x} $x %k"
  local tmpl="$fg $bg %F{0}$bg%f"
  print -Pu2 -- $tmpl "%B${tmpl}%b"
}

rgbcolor() { hexcolor $(printf "#%02x%02x%02x" ${1:-0} ${2:-0} ${3:-0}) }

colors16() {
  local fgc bgc
  for i in {0..15}; do
    fgc="${fgc} %F{$i}${(l:2::0:)i}%f "
    bgc="${bgc}%K{$i}${(l:4:)}%k"
    if (( (i + 1) % 16 == 0 )); then
      print -lP $fgc $bgc
      fgc='' bgc=''
    fi
  done
  print -lP $fgc $bgc
}

function colors256() {
  for i in {16..231}; do
    fg=$(( 231 + ((i - 16) % 36 / 18) ))
    print -Pn -- "%F{$fg}%K{$i} ${(l:3:: :)i} %f%k"
    (( (i - 15) % 36 )) || print
  done
  print
}

function colors256plain() {
  for i in {16..231}; do
    fg=$(( 231 + ((i - 16) % 36 / 18) ))
    print -Pn -- "%F{$fg}%K{$i}     %f%k"
    (( (i - 15) % 36 )) || print
  done
  print
}
