#!/usr/bin/env zsh
#
# Helper function for dotfiles management with fzf.

emulate -L zsh -o no_unset -o pipe_fail -o extended_glob -o pushd_silent

local GIT_DIR GIT_WORK_TREE
unset GIT_DIR GIT_WORK_TREE

0="${${ZERO:-${0:#$ZSH_ARGZERO}}:=${(%):-%N}}"
_fn_name="${0:t}"

function _printinfo() { print -Pu2 -- "%F{blue}%B${_fn_name}:%f%b" "$@"; }
function _printerr()  { print -Pu2 -- "%F{red}%B${_fn_name}:%f%b" "$@"; }


{
  pushd -q ~ || return

  local -x GIT_DIR="$HOME/.dotfiles"
  local -x GIT_WORK_TREE="$HOME"

  if ! git rev-parse --is-inside-work-tree &>/dev/null; then
    _printerr 'Not in a git repo'
    return 1
  fi

  git ls-files --full-name --directory $GIT_WORK_TREE | fzf \
    --preview 'bat --color=always {}' \
    --preview-window 'right,75%' \
    --bind 'enter:become(nvim {})'

} always {
  unset -f -- _printinfo
  unset -f -- _printerr
  popd -q
}
